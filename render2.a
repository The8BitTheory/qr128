; this renders 2x2 modules in a single petscii character
; this allows for the full size of the max supported size (version 5 with 37x37 modules) on all machines
; including the vic 20

render

!zone render
    .m_flipflop     = streamLength
    .m_compWritePos = contentLength

    jsr set_location_to_data
    ldy #0                    ;y holds the readcolumn (reset to zero after eol is reached)
    
    ldx #0
    stx z_counter1            ;used for counting modules for display in a single character (used for bit shifting)
    stx z_counter2            ;z_counter2 temporarily holds the value of the 4 modules (the bit-shifted values)
    stx .m_compWritePos         ;in this context, this should be read as "write position"
    inc .m_compWritePos
    
    clc
    lda #<data
    adc matrixSize
    sta z_location3
    lda #>data
    adc matrixSize+1
    sta z_location3+1
    
    lda size
    sta (z_location3),y
    sta m_ypos                ;used to check whether we moved below the last line
    lda #0
    sta m_xpos                ;the column for writing the petscii character that holds 4 modules
    
.read_4_modules
    jsr .handle_module
    inc z_counter1
    iny
    cpy size  ; check if we're at the right border of the matrix
    beq +     ; if yes, read the module below
    
    jsr .handle_module
+   inc z_counter1
    dey
    jsr inc_line
    dec m_ypos
    beq .skip_bottom_row    ;we're in the last line. go up and two to the right (dec_line once and iny twice)

    jsr .handle_module
    inc z_counter1
    iny
    cpy size
    beq .next_line
    
    jsr .handle_module
    iny
    jsr dec_line
    inc m_ypos
    
    jsr .write_character    

    
    jmp .read_4_modules

.skip_bottom_row
    jsr dec_line
    inc m_ypos
    iny
    cpy size
    beq .end
    iny
    jsr .write_character
    jmp .read_4_modules
    
.end
    rts
    
    
.next_line
    jsr .write_character
    
    ldy #0
    jsr inc_line
    dec m_ypos
    jsr .inc_line2
    
    jmp .read_4_modules
    
    
.handle_module
    lda (z_location),y
    bit .m_64   ;is xor-bit set?
    bvc .collect_value
    
    pha
    and #1
    sta z_temp
    pla
    and .m_maskbit
    bne +
    lda #0
    jmp ++
+   lda #1
++  eor z_temp

.collect_value
    ;store final value of module in bit5 (ready to be rendered)
    ;if module is dark, set bit 5 (if not, leave it untouched)
;    bit stream2module.m_one
;    beq +   

;    pha
;    lda #32
;    ora (z_location),y
;    sta (z_location),y
;    pla

    ldx z_counter1
-   beq +
    asl
    dex
    jmp -
    
+   ora z_counter2
    sta z_counter2

    rts
    
.write_character
    sty z_temp
    
    ldx z_counter2
    lda .modulechars,x
    ldy m_xpos
    sta (z_location2),y
    
    ; write module character index to memory for later saving to disk
    
    lda .m_flipflop
    bit stream2module.m_one  ;are we in an even or odd column?
    bne +                   ;bne means bit set -> odd column -> OR value to memory with already written high nybble
    
    ldy .m_compWritePos
    txa                       ;this needs to be written as the high nybble, shift it left
    asl
    asl
    asl
    asl
    jmp ++                    ;don't OR, to keep the lower nybble clear for the next index-value
    
+   txa
    ldy .m_compWritePos
    ora (z_location3),y       ;OR because this is the lower nybble and we need to combine it with the upper
    sec
    
++  sta (z_location3),y
    bcc +
    inc .m_compWritePos
    
+   ldx #0
    stx z_counter1
    stx z_counter2
    
    inc .m_flipflop
    inc m_xpos
    ldy z_temp
    rts
    

    
.inc_line2
    lda #0
    sta m_xpos
    
    lda z_location2
    clc
    adc rsDivisorOffset
    sta z_location2
    bcc +
    inc z_location2+1
        
+   rts

.modulechars  !byte 32,126,124,226,123,97,255,236,108,127,225,251,98,252,254,160
.m_64         !byte 64
.m_maskbit    !byte 2