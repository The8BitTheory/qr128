#RetroDevStudio.MetaData.BASIC:2049,BASIC V2,uppercase,10,10
#10 T$="HTTPS://WWW.FORUM64.DE/INDEX.PHP?THREAD/141120-HALLOWEEN-LEVTY-SPEZIAL-GRUSELSCHOCKER-TURNIER/"
5 IFA=0THENA=-1:LOAD"QR64.BIN",8,1
10 T$="HTTPS://WWW.FORUM64.DE/"
20 PA=49152

35 PRINT "CALLING QR-CODE GENERATION...";
40 S=TI:POKE 780,ASC("T"):SYSPA

45 PRINT"CALLING P2A...";
50 S=TI:SYSPA+3:S=TI-S
51 PRINT"TOOK "S" JIFFIES"
55 PRINT"CALLING BYTES2STREAM"
60 S=TI:SYSPA+6:S=TI-S
61 PRINT"TOOK "S" JIFFIES"
65 PRINT"CALLING CALC-XOR-MASKS"
70 S=TI:SYSPA+9:S=TI-S
71 PRINT"TOOK "S" JIFFIES"
75 PRINT"CALLING RS"
80 S=TI:SYSPA+12:S=TI-S
81 PRINT"TOOK "S" JIFFIES"
85 PRINT"CALLING WRITE PATTERNS"
90 S=TI:SYSPA+15:S=TI-S
91 PRINT"TOOK "S" JIFFIES"
95 PRINT"CALLING STREAM2MODULE"
100 SYSPA+18:S=TI-S
101 PRINT"TOOK "S" JIFFIES"
105 PRINT"EXPORTING ADRESSES"
110 SYSPA+21



115 A=PEEK(780):X=PEEK(781):Y=PEEK(782)
119 S=TI-S

120 PRINT "TOOK "S" JIFFIES"
130 DEF FN XR(ZZ)=(DV OR XV)-(DV AND XV)

150 F=X+Y*256:L=A
160 PRINT "START OF MATRIX="F", L="L
162 PRINT "XORING WITH PATTERN IN BIT 1"

170 O=F:C=0:R=0:POKE53280,1:POKE53281,1:POKE646,0:PRINT" ";

#200 IF L<=25 THEN 1000
210 GOTO 6000

######
# PRINT FULL MODULES
#########
# MV:MASK-VALUE (2 MEANS BIT 1, BECAUSE 2BIT)
# DV:DATA-VALUE
# XV:XMASK-VALUE
# DV AND XV ARE XORED

1000 MV=2
1005 FOR Y=1 TO L
1010  FOR X=1 TO L

1020   PO=PEEK(O)
1021   DV=ABS((PO AND 1)=1)
1022   XV=ABS((PO AND MV)=MV)
1023   V=FNXR(0)

1030   IF V=1 THEN PRINT CHR$(18)" "CHR$(146);:GOTO 1040
1031   PRINT " ";

1040   O=O+1
1050   IF X = L THEN IF Y<L THEN PRINT" ":PRINT" ";

1060  NEXT
1065  IF Y<L THEN 1070
1066  GETI$:IFLEN(I$)=0THEN1066
1070 NEXT

1075 


1090 END



######
# PRINT 4 MODULES PER CHARACTER
########



6000 REM OUTPUT MATRIX TO SCREEN
6103 DIM CH$(15)
6104 CH$(0)=CHR$(32):CH$(1)=CHR$(190):CH$(2)=CHR$(188):CH$(3)=CHR$(18)+CHR$(162)+CHR$(146)
6105 CH$(4)=CHR$(187):CH$(5)=CHR$(161):CH$(6)=CHR$(18)+CHR$(191)+CHR$(146):CH$(7)=CHR$(18)+CHR$(172)+CHR$(146)
6106 CH$(8)=CHR$(172):CH$(9)=CHR$(191):CH$(10)=CHR$(18)+CHR$(161)+CHR$(146):CH$(11)=CHR$(18)+CHR$(187)+CHR$(146)
6107 CH$(12)=CHR$(162):CH$(13)=CHR$(18)+CHR$(188)+CHR$(146):CH$(14)=CHR$(18)+CHR$(190)+CHR$(146):CH$(15)=CHR$(18)+CHR$(32)+CHR$(146)



#6110 GOTO 6300
# R:ROW
# C:COL
# O:OFFSET
# F:START OF MATRIX
# G:END OF MATRIX
# RENDERING OPTIONS
# - SHOW ADMINISTRATIVE PATTERNS AND DATASTREAM (NO XORING): 0
# - SHOW XOR WITH EVERY MASK (4) : 1-4
# - SHOW MASKS INDIVIDUALLY (4) : A-D



6220 FT=4
6221 DEF FN PA(ZZ)=((PO AND ZZ)=ZZ)

6222 FOR ZZ = 0 TO 2

6229  PO=PEEK(O):DV=POAND1:XV=ABS((PO AND FT)=FT)
6230  IF ((PO AND 64)=64) THEN V1=FNXR(0):GOTO 6235
6231  V1=DV

6235  PO=PEEK(O+1):DV=POAND1:XV=ABS((PO AND FT)=FT)
6236  IF ((PO AND 64)=64) THEN V2=FNXR(0):GOTO 6240
6237  V2=DV

6240  PO=PEEK(O+L):DV=POAND1:XV=ABS((PO AND FT)=FT)
6241  IF ((PO AND 64)=64) THEN V3=FNXR(0):GOTO 6245
6242  V3=DV

6245  PO=PEEK(O+1+L):DV=POAND1:XV=ABS((PO AND FT)=FT)
6246  IF ((PO AND 64)=64) THEN V4=FNXR(0):GOTO 6250
6247  V4=DV

6250  IF C<L-1 THEN V=V1+V2*2+V3*4+V4*8:GOTO 6255
6251  V=V1+V3*4

6255  PRINT CH$(V);
6260  O=O+2
6265  C=C+2:IF C >= L THEN O=O+L-1:C=0:R=R+1:PRINT" ":PRINT" ";
6266  IF R<(L-1)/2 THEN ZZ=0
6270 NEXT

# LAST ROW IS HANDLED INDIVIDUALLY
6271 FOR Y=0TO(L-1)/2

6273  PO=PEEK(O):DV=POAND1:XV=ABS((PO AND FT)=FT)
6274  IF ((PO AND 64)=64) THEN V1=FNXR(0):GOTO 6277
6275  V1=DV

6277  PO=PEEK(O+1):DV=POAND1:XV=ABS((PO AND FT)=FT)
6278  IF ((PO AND 64)=64) THEN V2=FNXR(0):GOTO 6281
6279  V2=DV

6281  IF C<L-1 THEN V=V1+V2*2:ELSE V=V1
6282  PRINT CH$(V);
6283  O=O+2:C=C+2
6284 NEXT
6285 PRINT " "

6290 PRINT


6400 END