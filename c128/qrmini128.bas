#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0,uppercase,10,10
3 DD=PEEK(186)
4 GRAPHIC1:GRAPHIC0:COLOR 0,2:COLOR4,2:COLOR5,1
5 BLOAD"QR.BIN",B0,U(DD)
6 H$="HTTPS://"
10 T$="GITHUB.COM/THE8BITTHEORY/QR128"

15 NV=83+16:DIM SC%(NV),AO%(NV),CO%(8)
16 DEF FN LB(ZZ)=ZZ-INT(ZZ/256)*256
17 DEF FN HB(ZZ)=INT(ZZ/256)

25 T$=H$+T$

35 GRAPHIC0:PRINT"GENERATING QR-CODE..."
40 S=TI

42 I=POINTER(T$)
44 BANK1:I1=PEEK(I+1):I2=PEEK(I+2):I=PEEK(I):BANK15

45 FAST:SYS DEC("1300"),I,I1,I2:SLOW:RREGA,X,Y:GRAPHIC5
46 F=X+Y*256:L=A:PRINT "DATA AT"F", L="L

114 S=TI-S

120 PRINT T$
121 PRINT "TOOK"S"JIFFIES"


150 PRINT "1-3 CHANGE MASK"
152 PRINT "S   SAVE RUNTIME"
155 PRINT "N   NEW QR-CODE"
156 PRINT "X   EXIT"

160 GET I$:IF I$="" THEN 160
161 IF ASC(I$)>48 THEN IF ASC(I$)<53 THEN BM=2(ASC(I$)-48):POKEDEC("1307"),BM:GOTO 35
162 IF I$="S" THEN 1000
163 IF I$="N" THEN 170
164 IF I$="X" THEN 199
169 GOTO 150

170 PRINT "URL TO ENCODE (WITHOUT "H$"):"
180 INPUT T$:IF T$="X" THEN END
190 GOTO 25

199 END


# SAVE FILE
1000 PRINT "PREPARING RUNTIME...";
1001 GOSUB 9000:AS=F:AE=AS+L:PRINT "DONE"
1005 PRINT "QRCODE FROM "AS" TO "AE" ("A" BYTES LONG)"

1100 INPUT "START ADDRESS OF QR-RUNTIME";I$
1105 INPUT "FILENAME";N$
1110 VL=VAL(I$)
1115 IF VL=49152 THEN 1140

1120 FOR X=0 TO 8
1122  V=VL+AO%(X)
1124  SC%(CO%(X))=FNLB(V):SC%(CO%(X)+1)=FNHB(V)
1126 NEXT

1140 BANK15

1200 OPEN1,DD,1,N$+",P,W"

# WRITE ADDRESS-BYTES
1202 PRINT#1,CHR$(FNLB(VL));CHR$(FNHB(VL));

# WRITE QR-VIEWER RUNTIME
1205 FOR X=0 TO NV
1206  O$=CHR$(SC%(X))
1210  PRINT#1,O$;
1215  PRINT".";
1220 NEXT
1221 PRINT:BANK0

# WRITE QR-CODE DATA
1222 FOR X=0 TO A:O$=CHR$(PEEK(AS+X))
1223  PRINT#1,O$;:PRINT".";
1224 NEXT
1225 PRINT
1230 BANK15:CLOSE1

1999 GOTO 150

##########################################
# BUILD BYTE ARRAY FOR GENERATING BINARY #
##########################################

9000 RESTORE
9010 FOR X=0TONV
9020  READ D
9030  SC%(X)=D
9040 NEXT

9105 CO=0
9110 FOR X=0TONV
9120  IF SC%(X)=192 THEN CO%(CO)=X-1:AO%(CO)=SC%(X-1):CO=CO+1
9140 NEXT

9199 RETURN



10000 DATA 134,253,132,254,133,252,160,0,162,0,189,100,192,232,134,251
10010 DATA 141,83,192,141,82,192,166,251,189,100,192,72,74,74,74,74
10020 DATA 32,48,192,104,176,9,41,15,32,48,192,230,251,208,231,96
10030 DATA 170,189,84,192,145,253,200,204,82,192,208,18,206,83,192,240
10040 DATA 15,165,253,24,101,252,133,253,144,2,230,254,160,0,24,96
10050 DATA 56,96,0,0,32,126

10055 DATA 124,226,123,97,255,236,108,127
10060 DATA 225,251,98,252,254,160

